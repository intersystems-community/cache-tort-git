<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="%SourceControl.Git.Utils">
<Abstract>1</Abstract>
<IncludeCode>%occStatus</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<TimeCreated>62657,53384.637236</TimeCreated>

<Parameter name="Storage">
<Default>^Git</Default>
</Parameter>

<Parameter name="InstallNamespace">
<Default>%SYS</Default>
</Parameter>

<Parameter name="SCListFilename">
<Description>
Name of the file with version controlled items</Description>
<Default>sc-list.txt</Default>
</Parameter>

<Parameter name="IsSYNC">
<Description><![CDATA[
If equals to 0 all tortoise-git calls are asynchronous<br/>
Otherwise calls are synchronous and import is automatically executed after git calls.]]></Description>
<Default>0</Default>
</Parameter>

<Parameter name="GitMenuItems">
<Default>,%Settings,%Commit,%Pull,%Fetch,%Push,%Diff,%RepoStatus,%Resolve,%Revert,%Log,</Default>
</Parameter>

<Parameter name="ImportAfterGitMenuItems">
<Default>,%Clone,%Commit,%Pull,%Fetch,%Push,%Resolve</Default>
</Parameter>

<Parameter name="GitContextMenuItems">
<Default>,%Diff,%Blame,</Default>
</Parameter>

<Method name="InstallNamespaceStorage">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[$Replace(..#Storage,"^","^["""_..#InstallNamespace_"""]")
]]></Implementation>
</Method>

<Method name="DefaultTemp">
<Description>
Returns root temp folder</Description>
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[$G(@..InstallNamespaceStorage()@("%defaultTemp"))
]]></Implementation>
</Method>

<UDLText name="T">
<Content><![CDATA[
// 8012 because this error has corresponding error message

]]></Content>
</UDLText>

<Method name="MakeError">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<FormalSpec>msg:%String</FormalSpec>
<Private>1</Private>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[$$$ERROR(8012,"Git",msg)
]]></Implementation>
</Method>

<Method name="TempFolder">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<Private>1</Private>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[..DefaultTemp()_$TR($znspace,"%")_"\"
]]></Implementation>
</Method>

<Method name="GitBinPath">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[""""_$G(@..InstallNamespaceStorage()@("%gitBinPath"))_""""
]]></Implementation>
</Method>

<Method name="NeedSettings">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[(..DefaultTemp() = "") ||  (..GitBinPath() = "")
]]></Implementation>
</Method>

<Method name="InstallNamespace">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[..#InstallNamespace
]]></Implementation>
</Method>

<Method name="UpdateSettings">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&settings]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim slash As %String = "\"
    
    // we change ^STORAGE to ^["InstallNamespace"]|STORAGE, so we store path to git.exe in one place
    s @..InstallNamespaceStorage()@("%gitBinPath") = settings("gitBinPath")
    
    // let's add slash in the end
    if settings("defaultTemp")'="" && ($E(settings("defaultTemp"),*)'=slash) {
        s settings("defaultTemp") = settings("defaultTemp")_slash
    }
    s @..InstallNamespaceStorage()@("%defaultTemp") = settings("defaultTemp")
]]></Implementation>
</Method>

<Method name="GitCommand">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<FormalSpec>commandName:%String,itemName:%String=""</FormalSpec>
<Implementation><![CDATA[
..GitBinPath()_" /command:"_commandName_
               " /path:"_..TempFolder()_
               $case(commandName,"clone":" /exactpath:"_..TempFolder(),:"")_
               $case(itemName,"":"",:..ExternalName(itemName))
]]></Implementation>
</Method>

<Method name="IsMenuGitCommand">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<FormalSpec>menuItemName:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[$F(..#GitMenuItems, ","_menuItemName_",") > 0
]]></Implementation>
</Method>

<Method name="IsContextMenuGitCommand">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<FormalSpec>menuItemName:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[$F(..#GitContextMenuItems, ","_menuItemName_",") > 0
]]></Implementation>
</Method>

<Method name="IsImportAfter">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<FormalSpec>menuItemName:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[$F(..#ImportAfterGitMenuItems, ","_menuItemName_",") > 0
]]></Implementation>
</Method>

<Method name="UserAction">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[InternalName:%String,MenuName:%String,&Target:%String,&Action:%String,&Reload:%Boolean]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim folder As %String = ..TempFolder()
    #dim menuName As %String = $piece(MenuName,",")
    #dim menuItemName As %String = $piece(MenuName,",",2)
    #dim ec As %Status = $$$OK
    
    if (menuName = "%SourceMenu") {
        if (menuItemName = "%Cache-Git-Settings") {
            s Action = 2
            #dim defNamespace As %String = ..#InstallNamespace
            s Target = ##class(%cspapp.gitprojectsettings).%GetParameter("CSPURL")_"?NSpace="_$znspace_"&Username="_$username
        }elseif (menuItemName = "%CreateRepo") {
            if ##class(%File).CreateDirectoryChain(..TempFolder()) {
                // cleanup items info
                k @..#Storage@("items")
                k @..#Storage@("TSH")
                s Action = 3
                s Target = ..GitCommand("repocreate")
            } else {
                s ec = ..MakeError("Unable to create folder "_..TempFolder())
            }
        }elseif (menuItemName = "%Export") {
            s ec = ..ExportAll()
            if ec {
                w "Экспорт программ выполнен",!
            }
        }elseif (menuItemName = "%Import") {
            s ec = ..ImportAll()
            s Reload = 1
        }elseif (menuItemName = "%Clone") {
            if ##class(%File).CreateDirectoryChain(..TempFolder()) {
                // cleanup items info
                k @..#Storage
                s Action = 3
                s Target = ..GitCommand("clone", "")
            } else {
                s ec = ..MakeError("Unable to create folder "_..TempFolder())
            }
        }elseif ..IsMenuGitCommand(menuItemName) {
                s Action = 3
                #dim command As %String = $ZCVT($E(menuItemName, 2, *), "L")
                s Target = ..GitCommand(command)
        }
    }elseif (menuName = "%SourceMenuContext") {
        if (menuItemName = "%AddToSC") {
            s ec = ..AddToSourceControl(InternalName)
        }elseif (menuItemName = "%RemoveFromSC") {
            s ec = ..RemoveFromSourceControl(InternalName)
        }
        if ..IsContextMenuGitCommand(menuItemName) {
            s Action = 3
            s command = $ZCVT($E(menuItemName, 2, *), "L")
            s Target = ..GitCommand(command, InternalName)
        }
    }
    if (..#IsSYNC = 1) && (Action = 3) {
        s Action = 0
        d $zf(-1, "set HOME="_..DefaultTemp()_"&"_Target)
    }
    if ..IsImportAfter(menuItemName) && (Action = 3) {
            if (..#IsSYNC = 1) {
                s ec = ..ImportAll()
            } else {
                w "Chose Import All menu after work with Git!", !
            }
    }

    q ec
]]></Implementation>
</Method>

<Method name="AfterUserAction">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[Type:%Integer,Name:%String,InternalName:%String,Answer:%Integer,Msg:%String="",&Reload:%Boolean]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[    q $$$OK
]]></Implementation>
</Method>

<Method name="IsNamespaceInGit">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[##class(%File).Exists(..TempFolder()_".git")
]]></Implementation>
</Method>

<Method name="ExternalName">
<ClassMethod>1</ClassMethod>
<FormalSpec>name:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
    #define BACKSLASH "\"
    s name = $TR(name,"/", $$$BACKSLASH)

    // lower case for extensions
    #dim extension As %String = $ZCVT($P(name,".",$L(name,".")),"L")
    s $P(name,".",$L(name,".")) = extension
    
    // we shall put classes in different folders
    if extension = "cls" {
        s name = $TR( $P(name,".", 1, $L(name,".")-1), ".", $$$BACKSLASH)_".cls"
    }
    
    // we shall delete csp-app from csp files
    if $E(name, 1) = $$$BACKSLASH {
        s $E(name, 1) = ""
    }
    if $P(name, $$$BACKSLASH, 1) = "csp" {
        s $P(name, $$$BACKSLASH, 1, 2) = "csp"
    }

    q $TR(name," *?","___")_".xml"
]]></Implementation>
</Method>

<Method name="AddToSourceControl">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim i As %Integer
    #dim ec As %Status = $$$OK
    f i = 1:1:$L(InternalName, ",") {
        #dim item As %String = $P(InternalName, ",", i)
        s @..#Storage@("items", item) = ""
        #dim sc As %Status =  ..ExportItem(item)
        if 'sc {
            s ec = $$$ADDSC(ec, sc)
        }
    }
    s ec = $$$ADDSC(ec, ..ExportSCList())
    q ec
]]></Implementation>
</Method>

<Method name="DeleteExternalsForItem">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim type As %String = ..Type(InternalName)
    #dim ec As %Status = $$$OK
    if (type = "prj") || (type = "pkg") || (type = "csp" && ..IsCspFolder(InternalName)) {
        // we delete complex items
        
        //get all item in files
        #dim itemsList
        $$$QuitOnError(..ListItemsInFiles(.itemsList))
        
        #dim item As %String = ""
        //for all item in files
        f  {
            s item = $O(itemsList(item))
            q:item=""
            
            //if item is not in sc -- delete file
            if '..IsInSourceControl(item) {
                #dim sc As %Status = ..DeleteExternalFile(item)
                if 'sc {
                    s ec = $$$ADDSC(ec, sc)
                }
            }
        }
    } else {
        s ec = ..DeleteExternalFile(InternalName)
    }
    q ec
]]></Implementation>
</Method>

<Method name="RemoveFromSourceControl">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim sc As %Status = $$$OK
    if $D(@..#Storage@("items", InternalName)) {
        k @..#Storage@("items", InternalName)
        s sc = ..DeleteExternalsForItem(InternalName)
        d ..RemoveFolderIfEmpty(..TempFolder())
        s sc = $$$ADDSC(sc, ..ExportSCList())
    } else {
        #dim parentElement As %String = ""
        if ..IsInSourceControl(InternalName, .parentElement) {
            s sc = ..MakeError("This element is contained in "_parentElement_" that tracked by SourceControl")
        }else {
            s sc = ..MakeError("Element is not in SourceControl")
        }
        
    }
    q sc
]]></Implementation>
</Method>

<Method name="IsCspFolder">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
    #dim extension = $P(InternalName, ".", $L(InternalName, "."))
    q:extension="csp" 0
    
    #dim filename = $system.CSP.GetFileName(InternalName_"/")
    q filename'="" && ##class(%File).DirectoryExists(filename)
]]></Implementation>
</Method>

<Method name="Type">
<Description><![CDATA[
pkg -- package<br/>
prj -- project<br/>
csp -- csp-page or csp-folder. See <Method>IsCspFolder</Method>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
    #dim extension As %String = $ZCVT($P(InternalName,".",$L(InternalName,".")),"L")
    #dim type As %String = extension
    
    if $E(InternalName, 1, 4) = "/csp" {
        s type ="csp"
    }

    q type
]]></Implementation>
</Method>

<Method name="NameWithoutExtension">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[$piece(InternalName, ".", 1, $length(InternalName,".")-1)
]]></Implementation>
</Method>

<Method name="IsClassInPackage">
<Description>
packageName without extension</Description>
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<FormalSpec>ClassName:%String,packageName:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[$E(ClassName, 1, $L(packageName)) = packageName
]]></Implementation>
</Method>

<Method name="IsItemInProject">
<Description>
projectName without extension</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String,projectName:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
    // we should check two cases
    // direct inclusion
    // inclusion in package or csp-folder that contained in project
    #dim type As %String = ..Type(InternalName)
    //w InternalName, "->"
    #dim name As %String = $case(type, "cls": ..NameWithoutExtension(InternalName), 
                                       "pkg": $TR(..NameWithoutExtension(InternalName), "/", "."), 
                                       "csp": $E(InternalName, 2, *),
                                       :InternalName)
    if $E(name) = "." && (type = "pkg") {
        s $E(name) = ""
    }
    
    //w name, " "
    #dim checkId = projectName_"||"_name_"||"_$ZCVT(type,"U")
    //w checkId
    #dim isItemInProject As %Boolean = ##class(%Studio.ProjectItem).%ExistsId(checkId)
    //w " ", isItemInProject, !
    
    #dim i As %Integer
    if 'isItemInProject && ((type = "cls") || (type="pkg")) {
        for i = 1:1:$L(name, ".") {
            s checkId = projectName_"||"_$P(name, ".", 1, i)_"||PKG"
            //w checkId, !
            if ##class(%Studio.ProjectItem).%ExistsId(checkId) {
                s isItemInProject = 1
                q
            }
        }
    }
    
    if 'isItemInProject && (type = "csp") {
        for i = 1:1:$L(name, "/") {
            s checkId = projectName_"||"_$P(name, "/", 1, i)_"||DIR"
            
            if ##class(%Studio.ProjectItem).%ExistsId(checkId) {
                s isItemInProject = 1
                q
            }
        }
    }
    q isItemInProject
]]></Implementation>
</Method>

<Method name="IsItemInCSPFolder">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<FormalSpec>InternalName:%String,cspFolder:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[$E(InternalName, 1, $L(cspFolder)) = cspFolder
]]></Implementation>
</Method>

<Method name="FindInPackages">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[InternalName:%String,&sourceControlItem:%String]]></FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
    #dim item As %String = ""
    #dim found As %Boolean = 0
    f  {
        s item = $O(@..#Storage@("items", item))
        q:item=""
        continue:..Type(item)'="pkg"
        #dim packageName As %String = ..NameWithoutExtension(item)

        if ..IsClassInPackage(InternalName, packageName) {
            s found = 1
            s sourceControlItem = packageName
            q
        }
    }
    q found
]]></Implementation>
</Method>

<Method name="FindInProjects">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[InternalName:%String,&sourceControlItem:%String]]></FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
    #dim item As %String = ""
    #dim found As %Boolean = 0
    f  {
        s item = $O(@..#Storage@("items", item))
        q:item=""
        continue:..Type(item)'="prj"
        #dim projectName As %String = ..NameWithoutExtension(item)
        
        if ..IsItemInProject(InternalName, projectName) {
            s found = 1
            s sourceControlItem = projectName
            q
        }
    }
    q found
]]></Implementation>
</Method>

<Method name="FindInCspFolders">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[InternalName:%String,&sourceControlItem:%String]]></FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
    #dim cspFolder As %String = ""
    #dim found As %Boolean = 0
    f  {
        s cspFolder = $O(@..#Storage@("items", cspFolder))
        q:cspFolder=""
        continue:'(..Type(cspFolder)="csp" && ..IsCspFolder(cspFolder))
        
        if ..IsItemInCSPFolder(InternalName, cspFolder) {
            s found = 1
            s sourceControlItem = cspFolder
            q 
        }
    }
    q found
]]></Implementation>
</Method>

<Method name="IsInSourceControl">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[InternalName:%String,&sourceControlItem:%String]]></FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
    #dim isInSourceControl As %Boolean = 1
    
    s isInSourceControl = $D(@..#Storage@("items",InternalName)) > 0
    if isInSourceControl {
        s sourceControlItem = InternalName
    }else {
        //if no direct reference maybe we have to look in packages, projects or csp-apps ?
        //We have three groups of routines
        //packages for classes
        //projects for everything
        //csp-folders for csp and static files

        #dim type As %String = ..Type(InternalName)
        if type = "cls" {
            s isInSourceControl = ..FindInPackages(InternalName, .sourceControlItem)
        } elseif type = "csp" {
            s isInSourceControl = ..FindInCspFolders(InternalName, .sourceControlItem)
        }
        
        // our last chance to find item -- let's look in projects
        if 'isInSourceControl {
            s isInSourceControl = ..FindInProjects(InternalName, .sourceControlItem)
        }
    }
    //w "checking ", $G(type), " ", InternalName, "=", isInSourceControl, !
    q isInSourceControl
]]></Implementation>
</Method>

<Method name="FullExternalName">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[..TempFolder()_..ExternalName(InternalName)
]]></Implementation>
</Method>

<Method name="NormalizeExtension">
<ClassMethod>1</ClassMethod>
<FormalSpec>name:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
    #dim extension = $P(name, ".", $L(name, "."))
    if $L(extension) <= 3 {
        s $P(name, ".", $L(name, ".")) = $zcvt(extension, "L")
    }
    q name
]]></Implementation>
</Method>

<Method name="RoutineTSH">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
    #dim type = ..Type(InternalName)
    //for csp-files (csp,js,html,css, all that stored in csp/...) we always check for changes in external file
    #dim tsh = $case(type,"csp":"",:$G(@..#Storage@("TSH", ..NormalizeExtension(InternalName))))
    if tsh = "" {
        #dim ts As %String = ##class(%RoutineMgr).TS(InternalName)
        if ts '= "" {
            // prj files have milliseconds in timestamp, so we crop them
            s tsh = $P($zdth(ts, 3),".",1)
        }
    }
    q tsh
]]></Implementation>
</Method>

<Method name="UpdateRoutineTSH">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String,tsh:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    s @..#Storage@("TSH", ..NormalizeExtension(InternalName)) = $G(tsh, $H)
    q $$$OK
]]></Implementation>
</Method>

<Method name="RemoveRoutineTSH">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    k @..#Storage@("TSH", ..NormalizeExtension(InternalName))
    q $$$OK
]]></Implementation>
</Method>

<Method name="DeleteExternalFile">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim fullName = ##class(Utils).FullExternalName(InternalName)
    #dim ec As %Status = $$$OK
    if ##class(%File).Exists(fullName) {
        s ec = ##class(%File).Delete(fullName)
        d ..RemoveRoutineTSH(InternalName)
        w fullName, " for ", InternalName, " deleted",!
    }
    Quit ec
]]></Implementation>
</Method>

<Method name="GetTempFileAndRoutineTS">
<Description>
if temp file for InternalName not found return "0,0" in tempFileTSH</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[InternalName:%String,&tempFileTSH:%String,&routineTSH:%String]]></FormalSpec>
<Implementation><![CDATA[
    #dim filename As %String = ..FullExternalName(InternalName)
    set tempFileTSH = ##class(%File).GetFileDateModified(filename)
    set routineTSH = ..RoutineTSH(InternalName)
    //file not found or path not found or some other error
    s:tempFileTSH<0 tempFileTSH = "0,0"
    set tempFileTSH = $zdt(tempFileTSH,3)
    set routineTSH = $zdt(routineTSH,3)
]]></Implementation>
</Method>

<Method name="IsTempFileOutdated">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
    d ..GetTempFileAndRoutineTS(InternalName,.tempFileTSH,.routineTSH)
    q routineTSH]tempFileTSH
]]></Implementation>
</Method>

<Method name="IsRoutineOutdated">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
    d ..GetTempFileAndRoutineTS(InternalName,.tempFileTSH,.routineTSH)
    q tempFileTSH]routineTSH
]]></Implementation>
</Method>

<Method name="FixProjectCspReferences">
<ClassMethod>1</ClassMethod>
<FormalSpec>projectName:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim loadedProject As %String = $P(projectName, ".", 1)
    // now we should fix reference to csp pages in project items
    // that is: if project was exported from USER and imported in SAMPLES
    // then all reference to csp/user/page.csp should be changed to csp/samples/page.csp
    #dim item As %String =""
    #dim oldCspApp As %String = ""
    #dim newCspApp As %String = ""
    f  {
        s item = $O(^oddPROJECT(loadedProject,"Items",item))
        q:item=""
        if $D(^oddPROJECT(loadedProject,"Items",item,"CSP")) {
            #dim newitem As %String = item
            s $P(newitem,"/",1,2) = $E($system.CSP.GetDefaultApp($znspace),2,*)
            if newitem '= item {
                s ^oddPROJECT(loadedProject,"Items",newitem,"CSP") = ^oddPROJECT(loadedProject,"Items",item,"CSP")
                k ^oddPROJECT(loadedProject,"Items",item,"CSP")
                s newCspApp = $E($system.CSP.GetDefaultApp($znspace),2,*)
                s oldCspApp = $P(item,"/",1,2)
            }
        }
    }
    q $$$OK
]]></Implementation>
</Method>

<Method name="LoadIfOutdated">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    //WRITE "Before Load Of: ", InternalName, " ", ..IsInSourceControl(InternalName), !
    //q:'..IsInSourceControl(InternalName) $$$OK
    #dim filename As %String = ..FullExternalName(InternalName)
    #dim fileTSH = ##class(%File).GetFileDateModified(filename)
    #dim sc As %Status = $$$OK

    if ..IsRoutineOutdated(InternalName) {
        s sc = $system.OBJ.Load(filename,"-l-d")
        if sc {
            s sc = ..UpdateRoutineTSH(InternalName, fileTSH)
            if ..Type(InternalName) = "prj" {
                s sc = $$$ADDSC(sc, ..FixProjectCspReferences(InternalName))
            }
        }
    } else {
            //WRITE "skip import -- same TS",!
    }
    Quit sc
]]></Implementation>
</Method>

<Method name="ListItemsInFiles">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&itemList,&err]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #define DoNotLoad 1
    $$$QuitOnError($system.OBJ.ImportDir(..TempFolder(),"*.xml","-d",.err,1, .itemList, $$$DoNotLoad))
    
    //change all csp/ names to /csp/ names
    #dim item As %String = "csp"
    f  {
        s item = $O(itemList(item))
        q:item=""
        q:$E(item, 1, 4)'="csp/"
        k itemList(item)
        s itemList("/"_item)=""
    }
    q $$$OK
]]></Implementation>
</Method>

<Method name="ImportRoutines">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim err, itemList
    
    kill err, itemList
    s err = 0   
    
    #dim ec As %Status = ..ListItemsInFiles(.itemList, .err)
    q:'ec ec
    
    #dim internalName As %String = ""
    f  {
        s internalName = $O(itemList(internalName))
        q:internalName=""
        #dim sc As %Status = ..LoadIfOutdated(internalName)
        if $$$ISERR(sc) {
            s ec = $$$ADDSC(ec, sc)
        }
    }
    
    //let's delete all items for which corresponding files had been deleted
    #dim item as %String = ""
    f  {
        s item = $O(@..#Storage@("TSH", item))
        q:item=""

        if '##class(%File).Exists(..FullExternalName(item)) {
            #dim type As %String = ..Type(item)
            #dim name As %String = ..NameWithoutExtension(item)
            #dim deleted As %Boolean = 1
            if type = "prj" {
                s ec = $$$ADDSC(ec, $system.OBJ.DeleteProject(name))
            }elseif type = "cls" {
                s ec = $$$ADDSC(ec, $system.OBJ.Delete(item))
            }elseif $LF($LB("mac","int","inc","bas","mvb", "mvi","dfi"), type) > 0 {
                s ec = $$$ADDSC(ec, ##class(%Routine).Delete(item))
            }elseif type = "csp" {
                #dim filename = $system.CSP.GetFileName(item)
                if ##class(%File).Exists(filename) && '##class(%File).Delete(filename) {
                    s ec = $$$ADDSC(ec, ..MakeError("Error while removing "_item))
                }
            } else {
                s deleted = 0
            }
            
            if deleted && ec {
                d ..RemoveRoutineTSH(item)
                w item, " was deleted", !
            } else {
                w "Error: could not delete ", item, !
            }
        }
    }
    
    w "import done", !
    q ec
]]></Implementation>
</Method>

<Method name="ExportRoutinesAux">
<ClassMethod>1</ClassMethod>
<FormalSpec>path:%String,sep:%String="",level:%Integer=0</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #define Dir
    #define OrderBy
    #define SystemFiles
    #define Flat
    #define NotStudio
    #define ShowGenerated 0
    #define Filter
    #define CspFile 5
    #define Directory 9
    #define CSPFolder 10
    
    #dim rs As %ResultSet = ##class(%ResultSet).%New("%RoutineMgr:StudioOpenDialog")
    #dim ec As %Status = rs.Execute(path_$case(path,"":"",:"/")_"*",$$$Dir, $$$OrderBy, $$$SystemFiles, $$$Flat, $$$NotStudio, $$$ShowGenerated, $$$Filter)
    q:'ec ec
    while rs.Next() {
        #dim name As %String = rs.Get("Name")
        #dim isdirectory As %String = rs.Get("IsDirectory")
        #dim type As %String = rs.Get("Type")
        
        if (type = $$$Directory) || (type = $$$CSPFolder) {
            #dim newpath As %String = $case(path,"":name,:path_isdirectory_name)
            #dim importedcnt As %Integer = 0
            d ..ExportRoutinesAux(newpath, isdirectory, level + 1)
        } else {
            #dim InternalName As %String = path_sep_name
            if (type = $$$CspFile) && ($E(InternalName) '= "/") {
                s InternalName = "/"_InternalName
            }
            s ec = ..ExportItem(InternalName)
        }
    }
    k rs
    q ec
]]></Implementation>
</Method>

<Method name="ExportItem">
<ClassMethod>1</ClassMethod>
<FormalSpec>InternalName:%String,expand:%Boolean=1</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim type = ..Type(InternalName)
    
    if type = "pkg" {
        $$$QuitOnError(..ExportRoutinesAux(..NameWithoutExtension(InternalName), "."))
    }elseif type = "prj" && expand {
        $$$QuitOnError(..ExportProject(..NameWithoutExtension(InternalName)))
        $$$QuitOnError(..ExportItem(InternalName, 0))
    }elseif (type = "csp") && ..IsCspFolder(InternalName) {
        $$$QuitOnError(..ExportRoutinesAux(InternalName , "/"))
    }else {
        if ..IsTempFileOutdated(InternalName) {
            w "exporting new version of  ", InternalName, !
            #dim filename As %String = ..FullExternalName(InternalName)
            $$$QuitOnError($system.OBJ.Export(InternalName, filename,"-d/diff"))
            $$$QuitOnError(..UpdateRoutineTSH(InternalName, $H))
        }
    }
    q $$$OK
]]></Implementation>
</Method>

<Method name="ExportProject">
<ClassMethod>1</ClassMethod>
<FormalSpec>project:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim rs As %ResultSet = ##class(%ResultSet).%New("%Studio.Project:ProjectItemsList")
    $$$QuitOnError(rs.Execute(project))
    #dim typesWithoutExtension As %List = $LB("CLS", "PKG")
    while rs.Next() {
        #dim name = rs.Get("Name")
        if $LF(typesWithoutExtension, rs.Get("Type")) {
            s name = name _ "." _ rs.Get("Type")
        }
        #dim ec As %Status = ..ExportItem(name)
        q:'ec
    }
    k rs
    q $$$OK
]]></Implementation>
</Method>

<Method name="ExportAll">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    $$$QuitOnError(..ExportRoutines())
    q ..ExportSCList()
]]></Implementation>
</Method>

<Method name="ImportAll">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    $$$QuitOnError(..ImportSCList())
    q ..ImportRoutines()
]]></Implementation>
</Method>

<Method name="ExportRoutines">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim item As %String = ""
    #dim ec As %Status = $$$OK
    f  {
        s item = $O(@..#Storage@("items",item))
        q:item=""
        s ec = ..ExportItem(item)
        q:'ec       
    }
    q ec
]]></Implementation>
</Method>

<Method name="ExportSCList">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim filename = ..TempFolder()_..#SCListFilename    
    #dim file As %File = ##class(%File).%New(filename)
    #dim item As %String = ""
    #dim defaultCspApp As %String = $system.CSP.GetDefaultApp($znspace)
    $$$QuitOnError(file.Open("WSN"))
    
    f  {
        s item = $O(@..#Storage@("items",item))
        q:item=""
        #dim fixedItem As %String = item
        if $E(fixedItem, 1, $L(defaultCspApp)) = defaultCspApp {
            s $E(fixedItem, 1, $L(defaultCspApp)) = "<cspapp>"
        }
        
        d file.WriteLine(fixedItem)
        
    }
    $$$QuitOnError(file.%Save())
    d file.Close()
    k file
    q $$$OK
]]></Implementation>
</Method>

<Method name="ImportSCList">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim filename = ..TempFolder()_..#SCListFilename
    #dim file As %File = ##class(%File).%New(filename)
    #dim defaultCspApp As %String = $system.CSP.GetDefaultApp($znspace)
    #dim eol As %Boolean
    
    $$$QuitOnError(file.Open("R", 10))
    
    #dim a 
    while 'file.AtEnd  {
        #dim s As %String = file.ReadLine(,,.eol)
        continue:s=""
        #dim item As %String = $Replace(s, "<cspapp>", defaultCspApp)
        s a(item) = ""
    }
    k @..#Storage("items")
    m @..#Storage("items") = a
    d file.Close()
    k file
    q $$$OK
]]></Implementation>
</Method>

<Method name="RemoveFolderIfEmpty">
<Description>
returns true if directory was deleted</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>path:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
    #dim rs As %Status = ##class(%ResultSet).%New("%File:FileSet")
    #define DirsFirst 1
    $$$QuitOnError(rs.Execute(path,,,$$$DirsFirst))
    #dim fileCount As %Integer = 0
    while rs.Next() {
        #dim fullname As %String = rs.Get("Name")
        #dim type As %String = rs.Get("Type")
        #dim name As %String = rs.Get("ItemName")
        #define IsDirectory(%type) %type="D"
        
        s fileCount = fileCount + 1
        q:'$$$IsDirectory(type)
        continue:name=".git"
        if ..RemoveFolderIfEmpty(fullname) {
            s fileCount = fileCount - 1
        }
    }
    if fileCount = 0 {
        d ##class(%File).RemoveDirectory(path)
    }
    k rs
    q 'fileCount
]]></Implementation>
</Method>
</Class>


<Class name="%SourceControl.Git">
<Import>%SourceControl.Git</Import>
<Super>%Studio.Extension.Base</Super>
<TimeCreated>62655,58027.787011</TimeCreated>

<XData name="Menu">
<Data><![CDATA[
<MenuBase>
<Menu Name="%SourceMenu" Type="0">
<MenuItem Name="%Cache-Git-Settings" />
<MenuItem Name="%Settings" />
<MenuItem Name="%CreateRepo" Save = "001"/>
<MenuItem Name="%Clone" Save = "001"/>
<MenuItem Name="%Commit" Save = "001"/>
<MenuItem Separator="true"/>
<MenuItem Name="%Pull" Save = "001"/>
<MenuItem Name="%Fetch" Save = "001"/>
<MenuItem Name="%Push" Save = "001"/>
<MenuItem Separator="true"/>
<MenuItem Name="%Diff" Save = "001"/>
<MenuItem Name="%RepoStatus" Save = "001"/>
<MenuItem Name="%Resolve" Save = "001"/>
<MenuItem Name="%Revert" Save = "001"/>
<MenuItem Name="%Log" Save = "001"/>
<MenuItem Separator="true"/>
<MenuItem Name="%Export" Save = "001" />
<MenuItem Name="%Import" Save = "001" />
</Menu>
<Menu Name="%SourceMenuContext" Type="1">
<MenuItem Name="%AddToSC"/>
<MenuItem Name="%RemoveFromSC"/>
<MenuItem Name="%Diff" Save = "001"/>
<MenuItem Name="%Blame" Save = "001"/>
</Menu>
</MenuBase>
]]></Data>
</XData>

<Method name="UserAction">
<FormalSpec><![CDATA[Type:%Integer,Name:%String,InternalName:%String,SelectedText:%String,&Action:%String,&Target:%String,&Msg:%String,&Reload:%Boolean]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim ec As %Status = $$$OK
	#dim menu As %Status = $Piece(Name, ",", 1)
	if menu '= "%SourceMenu", menu'="%SourceMenuContext" {
		q $$$OK
	}
	s ec = ##class(Utils).UserAction(InternalName, Name, .Target, .Action, .Reload)
	q ec
]]></Implementation>
</Method>

<Method name="OnSourceMenuItem">
<FormalSpec><![CDATA[name:%String,&Enabled:%String,&DisplayName:%String]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	if name = "%Cache-Git-Settings" {
		// We always show Settings
		s DisplayName = "Settings"
		q $$$OK
	} 
	if name = "%Settings" {
		s DisplayName = "TortoiseGit Settings"
		q $$$OK
	}
	
	if ##class(Utils).NeedSettings() {
		s Enabled = -1
		q $$$OK
	}
	
	if ##class(Utils).IsNamespaceInGit() {
		if name = "%Export" {
			s DisplayName = "Export All"
		} elseif name = "%Import" {
			s DisplayName = "Import All"
		} elseif name = "%RepoStatus" {
			s DisplayName = "Check for modifications"
		}elseif ##class(Utils).IsMenuGitCommand(name) {
			s DisplayName = $E(name, 2, *)
		} 
		else {
			s Enabled = -1
		}
	} else { 
		if name = "%CreateRepo" {
			s DisplayName = "Create Repo"
		} elseif name = "%Clone" {
			s DisplayName = "Clone"
		} else {
			s Enabled = -1
		}
	}
	q $$$OK
]]></Implementation>
</Method>

<Method name="OnSourceMenuContextItem">
<FormalSpec><![CDATA[itemName:%String,menuItemName:%String,&Enabled:%String,&DisplayName:%String]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	s:menuItemName="%AddToSC" DisplayName = "Add to SourceControl"
	s:menuItemName="%RemoveFromSC" DisplayName = "Remove from SourceControl"
	
	if (itemName = "") || '##class(Utils).IsNamespaceInGit() {
		s Enabled = -1
	}elseif ##class(Utils).IsInSourceControl(itemName) {
		s Enabled = $case(menuItemName, "%AddToSC":-1,:1)
	} else {
		s Enabled = $case(menuItemName, "%AddToSC":1,:-1)
	}
	q $$$OK
]]></Implementation>
</Method>

<Method name="OnMenuItem">
<Description><![CDATA[
This is called for every menu item returned to Studio to allow the menu to be enabled/disabled without
having to write a custom query for <query>MenuItems</query>. The <var>DisplayName</var> of this menu is
also passed by reference and this may be modified to change the appearance of this menu item. The <var>MenuName</var>
is the main menu name then the submenu name separated by a ','. If <var>Enabled</var> is set to -1 then it will remove
this menu item from the list totally, 0 will gray the menu item out and the default 1 will display the menu item as normal.]]></Description>
<FormalSpec><![CDATA[MenuName:%String,InternalName:%String,SelectedText:%String,&Enabled:%Boolean,&DisplayName:%String]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim menu As %String= $piece(MenuName,",")
	#dim name As %String = $piece(MenuName,",",2)
	if menu = "%SourceMenuContext", name = "" {
		s DisplayName = "Git"
	}
	if menu = "%SourceMenu", name = "" {
		s DisplayName = "Git"
	}
	#dim ec As %Status = $$$OK

	if menu = "%SourceMenu" {
		s ec = ..OnSourceMenuItem(name, .Enabled, .DisplayName)
	}elseif menu = "%SourceMenuContext" {
		s ec = ..OnSourceMenuContextItem(InternalName, name, .Enabled, .DisplayName)
	}
	Quit ec
]]></Implementation>
</Method>

<Method name="OnBeforeLoad">
<Description>
This is called before the actual load of data to give the chance
to load the item from an external format.</Description>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	if ##class(Utils).IsInSourceControl(InternalName) {
		q ##class(Utils).LoadIfOutdated(InternalName)
	}
	q $$$OK
]]></Implementation>
</Method>

<Method name="OnAfterSave">
<Description>
This is called after the item has been saved to the database.
It may be passed a reference to the object representing the item
just saved. It can be use to export this documement to an external form for example.</Description>
<FormalSpec>InternalName:%String,Object:%RegisteredObject=$$$NULLOREF</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	if ##class(Utils).IsNamespaceInGit() && ..IsInSourceControl(InternalName) {
		$$$QuitOnError(##class(Utils).RemoveRoutineTSH(InternalName))
		q ##class(Utils).ExportItem(InternalName)
	} else {
		q $$$OK
	}
]]></Implementation>
</Method>

<Method name="IsInSourceControl">
<Description>
Returns true if this item is in source control and false otherwise.</Description>
<CodeMode>expression</CodeMode>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[InternalName'="" && ##class(Utils).IsInSourceControl(InternalName)
]]></Implementation>
</Method>

<Method name="OnAfterDelete">
<Description>
Called after an item is deleted.</Description>
<FormalSpec>InternalName:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	if ##class(Utils).IsInSourceControl(InternalName) {
		q ##class(Utils).DeleteExternalFile(InternalName)
	}
	q $$$OK
]]></Implementation>
</Method>
</Class>


<CSP name="gitprojectsettings.csp" application="/csp/sys/" default="1"><![CDATA[
<html>
<head>
<title>Cache-Git Settings</title></head>

<body>
<server>
 if $D(%request.Data("gitsettings",1)) {
	 k settings
	 for param="gitBinPath","defaultTemp"{
		 s settings(param) = $G(%request.Data(param,1))
	 } 
	 d ##class(%SourceControl.Git.Utils).UpdateSettings(.settings)
 }
 s gitBinPath = ##class(%SourceControl.Git.Utils).GitBinPath()
 s defaultTemp = ##class(%SourceControl.Git.Utils).DefaultTemp()
 s:defaultTemp="" defaultTemp =  "c:\temp\"
</server>
<form method='post'>
<input type="hidden" name="gitsettings" value="1" />
<h1>Settings</h1>
<table><tr>
<td>Path to tortoiseproc.exe<br/>(e.g. c:\tortoisegit\bin\tortoiseproc.exe)</td><td><input type="text" name="gitBinPath" size=40 value='#(..EscapeHTML(gitBinPath))#'/></td>
</tr><tr>
<td>Path to temp folder<br/>(e.g. c:\temp)</td><td><input type="text" name="defaultTemp" size=40 value='#(..EscapeHTML(defaultTemp))#'/></td>
</tr>

</table>
<input type='submit' value = 'Save!'/>
</form>
<csp:if condition='$D(%request.Data("gitsettings",1)) && (##class(%SourceControl.Git.Utils).NeedSettings() = 0)'>
	<em>Click cross in the upper-right corner to close the settings window.</em>
</csp:if>
</body>
</html>]]></CSP>
</Export>
